import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'com.palantir.git-version' version "0.11.0"
    id "org.jetbrains.intellij" version "0.6.5"
}

repositories {
    jcenter()
    mavenCentral()
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "com.jetbrains.php.blade:${bladePluginVersion}",
            'CSS',
            'java-i18n',
            'properties'
    ]
    pluginName 'Laravel Plugin'
}

patchPluginXml {
    sinceBuild '173'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
}

group 'de.espend.idea.laravel'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '6.8.2'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
