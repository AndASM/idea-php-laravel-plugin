import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.6.5"
}

repositories {
    jcenter()
    mavenCentral()
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version platformVersion
    type platformType
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "com.jetbrains.php.blade:${bladePluginVersion}",
            'CSS',
            'java-i18n',
            'properties'
    ]
    pluginName pluginName_
}

patchPluginXml {
    sinceBuild '191'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
}

group 'de.espend.idea.laravel'
version pluginVersion

wrapper {
    gradleVersion '6.8.2'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
